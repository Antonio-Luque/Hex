; ----------------------------------------------------------------------------------------------------------------------
; Sound.z80asm
; Coded with BeepFX player tool by Shiru: https://opengameart.org/content/zx-spectrum-beeper-sound-effects
;
; Adapted for Hex game by Antonio Luque
; ----------------------------------------------------------------------------------------------------------------------

; ----------------------------------------------------------------------------------------------------------------------
; Sound_FX: play a sound effect (tone) defined by many parameters
; 
; Input:    IX = tone parameters address
;
; Output:   -
;
; Destroys: A, BC, DE, HL, IY
; ----------------------------------------------------------------------------------------------------------------------
Sound_FX:
                        di                                  ; disable interrupts

                        ld      c, (ix+0)                   ; BC = frames
                        ld      b, (ix+1)
                        ld      e, (ix+2)                   ; DE = frame length
                        ld      d, (ix+3)
                        push    de                          ; (simulate 'ld iy,de')
                        pop     iy                          ; store frame length in IY
                        ld      e, (ix+4)                   ; DE = pitch
                        ld      d, (ix+5)
                        ld      hl, 0                       ; reset HL
Sound_FX_Frames
                        push    bc                          ; store frames
                        push    iy                          ; (simulate 'ld bc,iy')
                        pop     bc                          ; restore frame length in BC
Sound_FX_FrameLength
                        add     hl, de                      ; HL = incremented pitch
                        ld      a, h                        ; set high-byte in A
                        cp      128                         ; compares with duty cycle, the timbre of the tone
                                                            ; (128 for 50% square wave)
                        sbc     a, a                        ; subtract with carry
                        and     16                          ; mask result
                        out     (254), a                    ; play sound
                        dec     bc                          ; decrement frame length
                        ld      a, b
                        or      c                           ; frame length = 0?
                        jr      nz, Sound_FX_FrameLength    ; no, repeat "frame length" times

                        ld      c, (ix+6)                   ; BC = pitch slide
                        ld      b, (ix+7)
                        ex      de, hl                      ; HL = pitch
                        add     hl, bc                      ; add pitch slide
                        ex      de, hl                      ; DE = incremented pitch
                        pop     bc                          ; restore frames
                        dec     bc                          ; decrement frames
                        ld      a, b
                        or      c                           ; frames = 0?
                        jr      nz, Sound_FX_Frames         ; no, repeat "frames" times

                        ei                                  ; enable interrupts
                        ret
