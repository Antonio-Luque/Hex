; ----------------------------------------------------------------------------------------------------------------------
; Play.z80asm
; Coded by Antonio Luque
; ----------------------------------------------------------------------------------------------------------------------

; ----------------------------------------------------------------------------------------------------------------------
; Play_Speccy: return the Speccy best move or first move from opening book
;
; Input:    -
; Output:   HL - points to board position of move in CYAN_BOARD
; Destroys: BC, DE, IX, IY, A
; ----------------------------------------------------------------------------------------------------------------------
Play_Speccy:
                        ; check for the first move (openning book)

                        ld      a, (MOVE_NUMBER)            ; get move number
                        dec     a                           ; is it CYAN first move?
                        jr      z, Play_Speccy_Book         ; yes, do first move as CYAN player

                        ; initialize BRAINIAC

                        ld      ix, CANDIDATES              ; IX points to candidates list 
                        ld      a, (GAME_LEVEL)             ; get game level
                        ld      (AI_DEPTH), a               ; save recursion depth level
                        call    PathFinder_Friend           ; get speccy side
                        call    BRAINIAC_best_move          ; get speccy best move

                        ; return BRAINIAC move

                        ld      l, e                        ; set node index in L
                        ld      h, CYAN_BOARD/256           ; HL points to board position
                        ret

Play_Speccy_Book        ; return move from opening book

                        ld      a, r                        ; get a pseudo-random number (0..127)
                        and     %00011111                   ; number is in range (0..31)
                        cp      27                          ; is it >= 27?
                        jr      nc, Play_Speccy_Book        ; yes, repeat until range is (0..26)
                        ld      hl, OPENINGS                ; HL points to opening book
                        ld      d, 0
                        ld      e, a
                        add     hl, de                      ; HL points to move index
                        ld      l, (hl)                     ; get move from opening book
                        ld      h, CYAN_BOARD/256           ; HL points to board position
                        ret
