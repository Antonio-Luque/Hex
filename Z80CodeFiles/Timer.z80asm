; ----------------------------------------------------------------------------------------------------------------------
; Timer.z80asm
; Coded by Antonio Luque
;
; Based on dissasembly of 'Chess' (c) 1982 Psion
; ----------------------------------------------------------------------------------------------------------------------

; ----------------------------------------------------------------------------------------------------------------------
; Timer_Interrupt: interrupt routine for manage players' timers
;
; Input:    -
; Output:   -
; Destroys: BC', DE', HL'
; ----------------------------------------------------------------------------------------------------------------------
Timer_Interrupt:
                        push    af                          ; store AF register
                        exx                                 ; store BC, DE and HL registers
                        ld      a, (TIMER_FLAG)             ; check timer flag
                        or      a                           ; is timer stopped?
                        jp      z, Timer_Exit               ; yes, exit routine
                        ld      hl, RED_TIMER               ; HL points to red's interrupt counter address
                        dec     a                           ; is red timer on?
                        jp      nz, Timer_Counters          ; yes, check interrupt counter 
                        ld      hl, CYAN_TIMER              ; HL points to cyan's interrupt counter address
Timer_Counters
                        dec     (hl)                        ; decrement interrupt counter
                        jp      nz, Timer_Exit              ; if not 0, exit routine
                        ld      (hl), 50                    ; restart interrupt counter
                        inc     hl                          ; move HL to seconds counter address
                        ld      a, 1                        ; increment seconds counter
                        add     a, (hl)
                        daa                                 ; convert to BCD (Binary-Coded-Decimal)
                        ld      (hl), a                     ; store seconds
                        cp      $60                         ; 60 seconds reached?
                        jp      nz, Timer_Show              ; no, show timer
                        ld      (hl), 0                     ; restart seconds counter
                        inc     hl                          ; move HL to minutes counter address
                        ld      a, 1                        ; increment minutes counter
                        add     a, (hl)
                        daa                                 ; convert to BCD (Binary-Coded-Decimal)
                        ld      (hl), a                     ; store minutes
Timer_Show
                        call    Timer_Display               ; display timer on the screen
Timer_Exit
                        exx                                 ; restore BC, DE and HL registers
                        pop     af                          ; restore AF register
                        ei                                  ; enable interrupts
                        ret

; ----------------------------------------------------------------------------------------------------------------------
; Timer_Display: display a timer on the screen
;
; Input:    -
; Output:   -
; Destroys: DE, BC, HL, A
; ----------------------------------------------------------------------------------------------------------------------
Timer_Display:
                        ld      de, RED_TIMER+2             ; DE points to red's minutes counter address
                        ld      bc, $48E2                   ; BC points to red's minutes screen address
                        ld      a, (TIMER_FLAG)             ; check timer flag
                        dec     a                           ; is cyan timer on?
                        jp      nz, Timer_Display_Min       ; no, jump to display minutes counter
                        ld      de, CYAN_TIMER+2            ; DE points to cyan's minutes counter address
                        ld      bc, $4078                   ; BC points to cyan's minutes screen address
Timer_Display_Min
                        call    Timer_Display_MinSec        ; display minutes counter on screen
                        dec     de                          ; DE points to seconds counter address
                        inc     c                           ; BC points to seconds screen address
Timer_Display_MinSec:
                        ld      a, (de)                     ; set min/sec counter in A
                        rra                                 ; move tens digit to the first 4th bits of A
                        rra
                        rra
                        rra
                        call    Timer_Display_Digit         ; display tens digit on screen
                        ld      a, (de)                     ; set min/sec counter in A 
Timer_Display_Digit:
                        and     %00001111                   ; discard the last 4th bits of the digit
                        push    bc                          ; store digit screen address
                        ld      hl, TIMER_DIGITS            ; HL points to TIMER_DIGITS address
                        add     a, a                        ; a digit is 8 bytes long
                        add     a, a
                        add     a, a
                        add     a, l                        ; set digit index in HL
                        ld      l, a
                        ld      a, (hl)                     ; get digit tile
                        ld      (bc), a                     ; put it on screen address
                        inc     b                           ; move BC to next pixel-line
                        inc     l                           ; move HL to next digit tile
                        ld      a, (hl)                     ; (repeat 7 more times)
                        ld      (bc), a
                        inc     b
                        inc     l
                        ld      a, (hl)
                        ld      (bc), a
                        inc     b
                        inc     l
                        ld      a, (hl)
                        ld      (bc), a
                        inc     b
                        inc     l
                        ld      a, (hl)
                        ld      (bc), a
                        inc     b
                        inc     l
                        ld      a, (hl)
                        ld      (bc), a
                        inc     b
                        inc     hl
                        ld      a, (hl)
                        ld      (bc), a
                        inc     b
                        inc     l
                        ld      a, (hl)
                        ld      (bc), a
                        pop     bc                          ; restore digit screen address
                        inc     c                           ; move screen address to next column
                        ret

; ----------------------------------------------------------------------------------------------------------------------
; Timer_Display_All: display cyan and red timers
;
; Input:    -
; Output:   A = 0
; Destroys: DE, BC, HL
; ----------------------------------------------------------------------------------------------------------------------
Timer_Display_All:
                        ld      a, 1                        ; set cyan timer on
                        ld      (TIMER_FLAG), a
                        call    Timer_Display               ; display cyan timer
                        ld      a, 2                        ; set red timer on
                        ld      (TIMER_FLAG), a
                        call    Timer_Display               ; display red timer
                        xor     a                           ; stop timers
                        ld      (TIMER_FLAG), a
                        ret

; ----------------------------------------------------------------------------------------------------------------------
; Timer_Reset: initialize cyan and red timers
;
; Input:    -
; Output:   -
; Destroys: HL
; ----------------------------------------------------------------------------------------------------------------------
Timer_Reset:
                        ld      hl, CYAN_TIMER              ; HL points to CYAN_TIMER address
                        ld      (hl), 50                    ; set interrupt counter (1/50 seconds)
                        inc     hl                          ; move HL to cyan's seconds counter
                        ld      (hl), 0                     ; reset seconds counter
                        inc     hl                          ; move HL to cyan's minutes counter
                        ld      (hl), 0                     ; reset minutes counter
                        inc     hl                          ; move HL to red's interrupt counter
                        ld      (hl), 50                    ; set interrupt counter (1/50 seconds)
                        inc     hl                          ; move HL to red's seconds counter
                        ld      (hl), 0                     ; reset seconds counter
                        inc     hl                          ; move HL to red's minutes counter
                        ld      (hl), 0                     ; reset minutes counter
                        ret
