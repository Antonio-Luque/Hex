; -----------------------------------------------------------------------------
; Frame.z80asm
; Coded by Antonio Luque
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; Draw_Frame: draw the board frame
;
; Input:     -
; Output:    -
; Destroys:  A, BC, HL, DE, IX
; -----------------------------------------------------------------------------
Draw_Frame:                  
                    ld      hl, $4566
                    call    Draw_Letters_Row
                    ld      hl, $516D
                    call    Draw_Letters_Row

                    call    Draw_Numbers
                    call    Vertical_Bars
                    call    Draw_Tokens
                    call    Draw_Timer_MinSec

                    ld      ix, FRAME_MAP
                    ld      hl, $4000
                    ld      c, 32
                    call    Draw_Frame_Loop
                    ld      hl, $50E0
                    ld      c, 32
Draw_Frame_Loop:
                    ld      a, (ix+0)
                    cp      -1
                    jr      z, draw_frame_next
                    push    hl
                    ld      h, 0
                    ld      l, a
                    add     hl, hl
                    add     hl, hl
                    add     hl, hl
                    ld      de, FRAME_TILES
                    add     hl, de
                    ex      de, hl
                    pop     hl
                    call    Draw_Tile
draw_frame_next
                    inc     l
                    inc     ix
                    dec     c
                    jr      nz, Draw_Frame_Loop
                    ret

; -----------------------------------------------------------------------------
Draw_Letters_Row:
                    ld      de, LETTERS
                    ld      c, 7
Draw_Letter_Loop
                    ld      b, 10
                    call    Draw_Item
                    inc     l
                    inc     l
                    dec     c
                    jr      nz, Draw_Letter_Loop
                    ret

; -----------------------------------------------------------------------------
Draw_Numbers:
                    ld      ix, NUMBERS_MAP
                    ld      c, 7
draw_numbers_loop
                    ld      l, (ix+0)
                    ld      h, (ix+1)
                    ld      b, 10
                    push    de
                    call    Draw_Item
                    pop     de
                    ld      a, l
                    add     a, 15
                    ld      l, a
                    ld      b, 10
                    call    Draw_Item
                    inc     ix
                    inc     ix
                    dec     c
                    jr      nz, draw_numbers_loop
                    ret

; -----------------------------------------------------------------------------
Draw_Tokens:
                    ld      hl, $44BA
                    ld      de, TOKEN
                    ld      b, 16
                    call    Draw_Item

                    inc     l
                    ld      b, 16
                    call    Draw_Item

                    ld      hl, $5424
                    ld      de, TOKEN
                    ld      b, 16
                    call    Draw_Item

                    inc     l
                    ld      b, 16

; -----------------------------------------------------------------------------
; Draw item
;
; Input:    HL = pixel-byte screen address
;           DE = sprite graph address
;           B  = item lenght
; Output:   B  = 0
; Destroys: A, DE
; -----------------------------------------------------------------------------
Draw_Item:
                    push    hl
Draw_Item_Loop
                    ld      a, (de)                 ; set sprite tile on screen
                    ld      (hl), a
                    inc     de                      ; next sprite tile
                    inc     h                       ; move to the next line
                    ld      a, h                
                    and     %00000111
                    jr      nz, Draw_Item_EOL  ; check EOL 1st third
                    ld      a, l
                    add     a, 32
                    ld      l, a
                    jr      c, Draw_Item_EOL   ; check EOL 2nd third
                    ld      a, h
                    sub     8
                    ld      h, a
Draw_Item_EOL
                    djnz    Draw_Item_Loop     ; repeat until full height
                    pop     hl
                    ret

; -----------------------------------------------------------------------------
Vertical_Bars:
                    ld      hl, $4020
                    ld      c, 7
                    call    Draw_Vertical_Bars
                    ld      hl, $4800
                    ld      c, 8
                    call    Draw_Vertical_Bars
                    ld      hl, $5000
                    ld      c, 7
Draw_Vertical_Bars:
                    ld      de, FRAME_TILES+72
                    push    de
                    call    Draw_Tile
                    ld      de, 31
                    add     hl, de
                    pop     de
                    call    Draw_Tile
                    inc     l
                    dec     c
                    jr      nz, Draw_Vertical_Bars
                    ret

; -----------------------------------------------------------------------------
Draw_Tile:
                    push    hl
                    ld      b, 8
draw_tile_loop
                    ld      a, (de)
                    ld      (hl), a
                    inc     de
                    inc     h
                    djnz    draw_tile_loop
                    pop     hl
                    ret

; -----------------------------------------------------------------------------
Draw_Timer_MinSec:
                    ld      hl, $407A
                    ld      (hl), 4
                    inc     h
                    ld      (hl), 4
                    ld      hl, $407D
                    ld      (hl), 10
                    inc     h
                    ld      (hl), 10
                    ld      hl, $48E4
                    ld      (hl), 4
                    inc     h
                    ld      (hl), 4
                    ld      hl, $48E7
                    ld      (hl), 10
                    inc     h
                    ld      (hl), 10
                    ret

; -----------------------------------------------------------------------------
Color_Frame:
                    ld      hl, $5801
                    ld      b, 6
color_frame_loop1
                    ld      (hl), a
                    inc     l
                    djnz    color_frame_loop1
                    ld      hl, $5812
                    ld      b, 13
color_frame_loop2
                    ld      (hl), a
                    inc     l
                    djnz    color_frame_loop2
                    ld      hl, $5800
                    ld      de, 31
                    ld      c, 3
color_frame_third
                    ld      b, 8
color_frame_loop3
                    ld      (hl), a
                    add     hl, de
                    ld      (hl), a
                    inc     l
                    djnz    color_frame_loop3
                    inc     h
                    dec     c
                    jr      nz, color_frame_third
                    ld      hl, $5AE1
                    ld      b, 16
color_frame_loop4
                    ld      (hl), a
                    inc     l
                    djnz    color_frame_loop4
                    ld      hl, $5AF6
                    ld      b, 9
color_frame_loop5
                    ld      (hl), a
                    inc     l
                    djnz    color_frame_loop5
                    ret

; -----------------------------------------------------------------------------
Color_Static_Items:
                    ld      hl, $5807
                    ld      (hl), 8
                    inc     l
                    ld      (hl), 71

                    ld      hl, $5810
                    ld      (hl), 71
                    inc     l
                    ld      (hl), 8

                    ld      hl, $5866
                    ld      de, 32
                    ld      b, 7
                    call    Color_Letters_Row
                    ld      hl, $5A6D
                    ld      b, 7
                    call    Color_Letters_Row

                    ld      hl, $58A5
                    ld      b, 7
color_numbers
                    ld      (hl), 2
                    ld      de, 15
                    add     hl, de
                    ld      (hl), 2
                    ld      de, 17
                    add     hl, de
                    ld      (hl), 2
                    ld      de, 15
                    add     hl, de
                    ld      (hl), 2
                    ld      de, 18
                    add     hl, de
                    djnz    color_numbers

                    ld      b, 3
                    ld      hl, $58BA
                    ld      de, 32  
color_token_blue
                    push    hl
                    ld      (hl), 5
                    inc     l
                    ld      (hl), 5
                    pop     hl
                    add     hl, de
                    djnz    color_token_blue

                    ld      b, 3
                    ld      hl, $5A24
                    ld      de, 32  
color_token_red
                    push    hl
                    ld      (hl), 2
                    inc     l
                    ld      (hl), 2
                    pop     hl
                    add     hl, de
                    djnz    color_token_red

                    ld      hl, $5898
                    ld      b, 6
color_player_blue
                    ld      (hl), 5
                    inc     l
                    djnz    color_player_blue

                    ld      hl, $5A02
                    ld      b, 6
color_player_red
                    ld      (hl), 2
                    inc     l
                    djnz    color_player_red

                    ld      hl, $5878
                    ld      b, 6
color_minsec_blue
                    ld      (hl), 7
                    inc     l
                    djnz    color_minsec_blue

                    ld      hl, $59E2
                    ld      b, 6
color_minsec_red
                    ld      (hl), 7
                    inc     l
                    djnz    color_minsec_red

                    ld      hl, $5918
                    ld      b, 6
color_message_blue
                    ld      (hl), 71
                    inc     l
                    djnz    color_message_blue

                    ld      hl, $5A82
                    ld      b, 6
color_message_red
                    ld      (hl), 71
                    inc     l
                    djnz    color_message_red
                    
                    ld      hl, $5AF1
                    ld      b, 5
color_level_mark
                    ld      (hl), 1
                    inc     l
                    djnz    color_level_mark
                    ret

; -----------------------------------------------------------------------------
Color_Letters_Row:
                    push    hl
                    ld      (hl), 5
                    add     hl, de
                    ld      (hl), 5
                    pop     hl
                    inc     l
                    inc     l
                    djnz    Color_Letters_Row
                    ret

; -----------------------------------------------------------------------------
FRAME_MAP:
                    defb      0,   1,   1,   1,   1,   1,   2,   3
                    defb      4,  -1,  -1,  -1,  -1,  -1,  -1,  -1
                    defb      5,   6,   7,   1,   1,   1,   1,   1
                    defb      1,   1,   1,   1,   1,   1,   1,   8
                    defb     10,   1,   1,   1,   1,   1,   1,   1
                    defb      1,   1,   1,   1,   1,   1,   1,   1
                    defb     11,   6,  14,  14,  14,   3,  12,   1
                    defb      1,   1,   1,   1,   1,   1,   1,  13

; -----------------------------------------------------------------------------
NUMBERS_MAP:
                    defw    $43A5, $43E6, $4B27, $4B68, $4BA9, $4BEA, $532B

; -----------------------------------------------------------------------------
FRAME_TILES:
                    defb    $00, $00, $1F, $3F, $3F, $3F, $3C, $3C              ; 00: up-left corner
                    defb    $00, $00, $FF, $FF, $FF, $FF, $00, $00              ; 01: horizontal bar
                    defb    $00, $00, $FF, $FE, $FE, $FF, $00, $00              ; 02: concave left
                    defb    $03, $07, $0F, $0F, $0F, $0F, $07, $03              ; 03: end frame 1
                    defb    $00, $01, $03, $03, $03, $03, $01, $00              ; 04: level left
                    defb    $00, $80, $C0, $C0, $C0, $C0, $80, $00              ; 05: level right
                    defb    $C0, $E0, $F0, $F0, $F0, $F0, $E0, $C0              ; 06: end frame 2
                    defb    $00, $00, $FF, $7F, $7F, $FF, $00, $00              ; 07: concave right
                    defb    $00, $00, $F8, $FC, $FC, $FC, $3C, $3C              ; 08: up-right corner
                    defb    $3C, $3C, $3C, $3C, $3C, $3C, $3C, $3C              ; 09: vertical bar
                    defb    $3C, $3C, $3F, $3F, $3F, $1F, $00, $00              ; 10: bottom-left corner
                    defb    $00, $00, $FE, $FF, $FF, $FE, $00, $00              ; 11: convex left
                    defb    $00, $00, $7F, $FF, $FF, $7F, $00, $00              ; 12: convex right
                    defb    $3C, $3C, $FC, $FC, $FC, $F8, $00, $00              ; 13: bottom-right corner
                    defb    $00, $00, $38, $7C, $7C, $38, $00, $00              ; 14: level marks

; -----------------------------------------------------------------------------
LETTERS:
                    defb    $3C, $7E, $FF, $C3, $F3, $C3, $D3, $C3, $7E, $3C    ; a
                    defb    $3C, $7E, $CF, $CF, $C3, $CB, $CB, $C3, $7E, $3C    ; b
                    defb    $3C, $7E, $FF, $E3, $CF, $CF, $CF, $E3, $7E, $3C    ; c
                    defb    $3C, $7E, $F3, $F3, $C3, $D3, $D3, $C3, $7E, $3C    ; d
                    defb    $3C, $7E, $FF, $E3, $CB, $C3, $CF, $E3, $7E, $3C    ; e
                    defb    $3C, $7E, $E3, $CF, $C7, $CF, $CF, $CF, $7E, $3C    ; f
                    defb    $3C, $7E, $FF, $E3, $D3, $D3, $E3, $F3, $46, $3C    ; g

; -----------------------------------------------------------------------------
NUMBERS:
                    defb    $3C, $7E, $E7, $C7, $E7, $E7, $E7, $E7, $7E, $3C    ; 1
                    defb    $3C, $7E, $E7, $D3, $F3, $E7, $CF, $C3, $7E, $3C    ; 2
                    defb    $3C, $7E, $C7, $F3, $E3, $F3, $F3, $C7, $7E, $3C    ; 3
                    defb    $3C, $7E, $D3, $D3, $C3, $F3, $F3, $F3, $7E, $3C    ; 4
                    defb    $3C, $7E, $C3, $CF, $C7, $F3, $F3, $C7, $7E, $3C    ; 5
                    defb    $3C, $7E, $E3, $CF, $C3, $CB, $CB, $C3, $7E, $3C    ; 6
                    defb    $3C, $7E, $C3, $F3, $F3, $E7, $E7, $E7, $7E, $3C    ; 7

