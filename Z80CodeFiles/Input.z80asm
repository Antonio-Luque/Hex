; ----------------------------------------------------------------------------------------------------------------------
; Input.z80asm
; Coded by Antonio Luque
; ----------------------------------------------------------------------------------------------------------------------

MAX_CHARS               EQU     2                           ; maximum of input chars

; ----------------------------------------------------------------------------------------------------------------------
; Input_Move: wait for the input of a movement through the keyboard
;
; Based on assembler course by sromero:
; https://wiki.speccy.org/cursos/ensamblador/gfx4_fuentes#lectura-de-texto-desde-teclado
;
; Input:    -
;
; Output:   -
;
; Uses:     TOKEN_COLOR (current token color)
;           STR_MOVE (formatted move string)
;
; Destroys: A, BC, DE, HL, IX
; ----------------------------------------------------------------------------------------------------------------------
Input_Move:
                        ld      bc, MAX_CHARS*256           ; B = maximum of input chars, C = chars counter

                        ld      ix, $5A83                   ; IX points to attribute address of RED input text
                        ld      hl, $5083                   ; HL points to screen address of RED input text
                        ld      a, (TOKEN_COLOR)            ; check current player color
                        cp      RED_COLOR                   ; is RED the current token color?
                        jr      z, Input_Start              ; yes, start input
                        ld      ix, $5919                   ; IX points to attribute address of CYAN input text
                        ld      hl, $4819                   ; HL points to screen address of CYAN input text
Input_Start
                        ld      (ix+0), WHITE_FLASH1        ; set attributes for cursor (flash)
                        ld      a, '_'                      ; text input cursor
                        call    Print_Char                  ; print cursor
Input_Loop
                        call    Input_Key_Scan              ; scan for a key pressed
                        cp      13                          ; is it ENTER?
                        jr      z, Input_Enter              ; yes, perform enter pressed
                        cp      '0'                         ; is it "delete"? (char '0' acts as delete, without Shift)
                        jr      z, Input_Delete             ; yes, perform delete char
                        ld      d, a                        ; store char in D
                        ld      a, c                        ; check number of chars inserted
                        cp      b                           ; chars limit reached?
                        jr      z, Input_Loop               ; yes, repeat input loop

                        or      a                           ; is it the first char?
                        jr      nz, Input_Numbers           ; no, perform input of second char

                        ; restrict first char to 'a'-'g'

                        ld      a, d                        ; restore char
                        cp      'a'                         ; is it < 'a'?
                        jr      c, Input_Loop               ; yes, repeat input loop (char is not allowed)
                        cp      'g'+1                       ; is it > 'g'?
                        jr      nc, Input_Loop              ; yes, repeat input loop (char is not allowed)

                        ; char is valid as column

                        ld      (STR_MOVE+0), a             ; save char in player move
                        ld      (ix+0), WHITE_FLASH0        ; restore attributes (no flash)
                        call    Print_Char                  ; print key pressed
                        inc     c                           ; increment chars counter
                        inc     l                           ; increment X position of current input screen location
                        inc     ixl                         ; increment X position of current input attr location
                        ld      a, '-'                      ; set dash char
                        call    Print_Char                  ; print dash char
                        inc     l                           ; increment X position of current input screen location
                        inc     ixl                         ; increment X position of current input attr location
                        jr      Input_Start                 ; get next char (do until ENTER is pressed)
Input_Numbers
                        ; restrict second char to '1'-'7'

                        ld      a, d                        ; restore char
                        cp      '1'                         ; is it < '1'?
                        jr      c, Input_Loop               ; yes, repeat input loop (char is not allowed)
                        cp      '7'+1                       ; is it > '7'?
                        jr      nc, Input_Loop              ; yes, repeat input loop (char is not allowed)

                        ; char is valid as row

                        ld      (STR_MOVE+3), a             ; save char in player move
                        ld      (ix+0), WHITE_FLASH0        ; restore attributes (no flash)
                        call    Print_Char                  ; print key pressed
                        inc     c                           ; increment chars counter
                        inc     l                           ; increment X position of current input screen location
                        inc     ixl                         ; increment X position of current input attr location
                        jr      Input_Start                 ; get next char (do until ENTER is pressed)

                        ; player pressed "delete" (char '0')
Input_Delete
                        ld      a, c                        ; get char counter
                        or      a                           ; is it 0?
                        jr      z, Input_Loop               ; repeat input loop (no chars to delete)

                        ld      (ix+0), WHITE_FLASH0        ; restore attributes (no flash)
                        cp      b                           ; is it the first char?
                        jr      z, Input_Delete_Char        ; yes, continue delete
                        ld      a, ' '                      ; empty char
                        call    Print_Char                  ; delete cursor and last char
                        dec     l                           ; decrement X position of input screen location
                        dec     ixl                         ; decrement X position of input attr location
Input_Delete_Char
                        dec     c                           ; decrement char counter
                        ld      a, ' '                      ; empty char
                        call    Print_Char                  ; delete cursor and last char
                        dec     l                           ; decrement X position of player input screen location
                        dec     ixl                         ; decrement X position of player input attr location
                        jr      Input_Start                 ; get next char

                        ; player pressed ENTER
Input_Enter
                        ld      a, c                        ; check char counter  
                        cp      b                           ; is it < MAX_CHARS?
                        jr      c, Input_Loop               ; yes, repeat input loop (move is incomplete)

                        ld      (ix+0), WHITE_FLASH0        ; restore attributes (no flash)
                        ld      a, ' '                      ; empty char
                        jp      Print_Char                  ; delete cursor and return via Print_Char

; ----------------------------------------------------------------------------------------------------------------------
; Input_Key_Scan: wait for a key pressed and return its ASCII code
;
;                           Bit
;  Port  |   0   |   1   |   2   |   3   |   4   |
;  ------+-------+-------+-------+-------+-------+
;  $FEFE | Shift |   Z   |   X   |   C   |   V   |
;  $FDFE |   A   |   S   |   D   |   F   |   G   |
;  $FBFE |   Q   |   W   |   E   |   R   |   T   |
;  $F7FE |   1   |   2   |   3   |   4   |   5   |
;  $EFFE |   0   |   9   |   8   |   7   |   6   |
;  $DFFE |   P   |   O   |   I   |   U   |   Y   |
;  $BFFE | Enter |   L   |   K   |   J   |   H   |
;  $7FFE | Space |  Sym  |   M   |   N   |   B   |
;
;  Bits are set to 0 for any key that is pressed and 1 for any key that is not pressed
;
; Input:    -
;
; Output:   A = ASCII code of key pressed
;
; Destroys: DE
; ----------------------------------------------------------------------------------------------------------------------
Input_Key_Scan:
                        push    bc                          ; store registers
                        push    hl
Input_Key_Released
                        ld      bc, $FEFE                   ; set keyboard port (see above)
Input_Key_Released_Line
                        in      a, (c)                      ; read port
                        cpl                                 ; invert bits
                        and     %00011111                   ; check 0 to 4 bits
                        jr      nz, Input_Key_Released      ; repeat from the beginning until no key is pressed
                        rlc     b                           ; move BC to the next port
                        jr      c, Input_Key_Released_Line  ; repeat for each port
                        ld      de, 5                       ; number of keys in each port (KEY_MAP displacement)
Input_Key_Pressed_Begin
                        ld      hl, KEY_MAP                 ; HL points to KEY_MAP array
Input_Key_Pressed_Line
                        in      a, (c)                      ; read port
                        cpl                                 ; invert bits
                        and     %00011111                   ; check 0 to 4 bits
                        jr      nz, Input_Key_Pressed       ; if any key is pressed, process it
                        add     hl, de                      ; HL points to the next KEY_MAP block
                        rlc     b                           ; move BC to the next port
                        jr      c, Input_Key_Pressed_Line   ; repeat for each port
                        jr      Input_Key_Pressed_Begin     ; repeat from the beginning until any key is pressed
Input_Key_Next
                        inc     hl                          ; move HL to the next character
Input_Key_Pressed
                        rra                                 ; rotate bits of port readed
                        jr      nc, Input_Key_Next          ; repeat until find the key pressed
                        ld      a, (hl)                     ; return key pressed (ASCII) in A
                        pop     hl                          ; restore registers
                        pop     bc
                        ret
